/**
 * Proto-typed standard library.
 * 
 * This file contains only non-internal functions,
 * not all functions in std library can be found here.
 * 
 * Author: Marek Sedlacek
 * Copyright (c) 2023 Marek Sedlacek
 * MIT licensed
 */

/***************************/
/*--- String functions ----*/
/***************************/

string mto_string(int? v) {
    if(v == none) {
        return "none"
    }
    int a = v
    return to_string(a)
}

string mto_string(float? v) {
    if(v == none) {
        return "none"
    }
    float a = v
    return to_string(a)
}

string mto_string(bool? v) {
    if(v == none) {
        return "none"
    }
    bool a = v
    return to_string(a)
}

string mto_string(string? v) {
    if(v == none) {
        return "none"
    }
    string a = v
    return a
}

string upper(string s) {
    string r
    for(string c : s) {
        int o = ord(c)
        if(o > 96 and o < 123) {
            r ++= chr(o ^ 32);
        }
        else {
            r ++= c
        }
    }
    return r
}

string lower(string s) {
    string r
    for(string c : s) {
        int o = ord(c)
        if(o > 64 and o < 91) {
            r ++= chr(o ^ 32);
        }
        else {
            r ++= c
        }
    }
    return r
}

string reverse(string s) {
    string r
    for(int i : length(s) - 1 .. -1) {
        r ++= s[i]
    }
    return r
}

/************************/
/*--- Math functions ---*/
/************************/

/** Ï€ (Ludolph's number) */
float M_PI  = 3.14159265358979323851280895940618620443274267017841339111328125

/** e (Euler's number) */
float M_E   = 2.71828182845904523542816810799394033892895095050334930419921875

/** Ï† (Golden ratio = (1 + 5^0.5)/2) */
float M_PHI = 1.618033988749894848207210029666924810953787527978420257568359375

/** Eulerâ€“Mascheroni constant */
float M_EGAMMA = 0.57721566490153286554942724251304753124713897705078125

